From 131503f51ed076529bddf8af113516c9975701cd Mon Sep 17 00:00:00 2001
From: Ryan Volz <rvolz@mit.edu>
Date: Thu, 31 Oct 2019 17:38:49 -0400
Subject: [PATCH] python: Do not link against python lib for building an
 extension module.

This fixes a segmentation fault when trying to use the python module on
OSX when built with conda (unsure why it doesn't arise otherwise).
Instead of linking against the python library, it is proper to not link
against the library and, for OSX builds, add linker options for
"-undefined" and "dynamic_lookup". This is precisely what the CMake
FindPython module does for linking against the Python::Module target.

See https://blog.tim-smith.us/2015/09/python-extension-modules-os-x
---
 cmake/Modules/GrPython.cmake | 29 +++++++++++++++++++++++++++++
 cmake/Modules/GrSwig.cmake   |  2 +-
 gr-qtgui/lib/CMakeLists.txt  |  7 +++++--
 3 files changed, 35 insertions(+), 3 deletions(-)

diff --git a/cmake/Modules/GrPython.cmake b/cmake/Modules/GrPython.cmake
index ff8264a9b..54eee8be8 100644
--- a/cmake/Modules/GrPython.cmake
+++ b/cmake/Modules/GrPython.cmake
@@ -68,6 +68,35 @@ else()
       )
 endif()
 
+# separarate target when linking to make an extension module, which should not
+# link explicitly to the python library on Unix-like platforms
+add_library(Python::Module INTERFACE IMPORTED)
+if(WIN32)
+    # identical to Python::Python, cannot be an alias because Python::Python is imported
+    # Need to handle special cases where both debug and release
+    # libraries are available (in form of debug;A;optimized;B) in PYTHON_LIBRARIES
+    if(PYTHON_LIBRARY_DEBUG AND PYTHON_LIBRARY_RELEASE)
+        set_target_properties(Python::Module PROPERTIES
+          INTERFACE_INCLUDE_DIRECTORIES "${PYTHON_INCLUDE_DIRS}"
+          INTERFACE_LINK_LIBRARIES "$<$<NOT:$<CONFIG:Debug>>:${PYTHON_LIBRARY_RELEASE}>;$<$<CONFIG:Debug>:${PYTHON_LIBRARY_DEBUG}>"
+        )
+    else()
+        set_target_properties(Python::Module PROPERTIES
+          INTERFACE_INCLUDE_DIRECTORIES "${PYTHON_INCLUDE_DIRS}"
+          INTERFACE_LINK_LIBRARIES "${PYTHON_LIBRARIES}"
+        )
+    endif()
+else()
+    set_target_properties(Python::Module PROPERTIES
+        INTERFACE_INCLUDE_DIRECTORIES "${PYTHON_INCLUDE_DIRS}"
+    )
+    if(APPLE)
+        set_target_properties(Python::Module PROPERTIES
+            INTERFACE_LINK_OPTIONS "LINKER:-undefined,dynamic_lookup"
+        )
+    endif(APPLE)
+endif(WIN32)
+
 
 ########################################################################
 # Check for the existence of a python module:
diff --git a/cmake/Modules/GrSwig.cmake b/cmake/Modules/GrSwig.cmake
index 39585ac4b..4118728cc 100644
--- a/cmake/Modules/GrSwig.cmake
+++ b/cmake/Modules/GrSwig.cmake
@@ -139,7 +139,7 @@ macro(GR_SWIG_MAKE name)
       )
     target_include_directories(${name} PUBLIC ${GR_SWIG_INCLUDE_DIRS})
     set_property(TARGET ${name} PROPERTY SWIG_DEPENDS ${GR_SWIG_TARGET_DEPS})
-    target_link_libraries(${name} Python::Python ${GR_SWIG_LIBRARIES})
+    target_link_libraries(${name} Python::Module ${GR_SWIG_LIBRARIES})
 endmacro(GR_SWIG_MAKE)
 
 ########################################################################
diff --git a/gr-qtgui/lib/CMakeLists.txt b/gr-qtgui/lib/CMakeLists.txt
index 6ee1fb723..25dd7faa3 100644
--- a/gr-qtgui/lib/CMakeLists.txt
+++ b/gr-qtgui/lib/CMakeLists.txt
@@ -91,8 +91,11 @@ endif(WIN32)
 
 include(GrPython)
 if(ENABLE_PYTHON)
-  target_compile_definitions(gnuradio-qtgui PUBLIC -DENABLE_PYTHON)
-  target_link_libraries(gnuradio-qtgui PUBLIC
+  # PRIVATE so users of the library can choose whether and how to link to Python
+  # (importantly, _qtgui_swig can avoid linking against Python and resolve its
+  #  own Python symbols at runtime while still using the Python-enabled qtgui)
+  target_compile_definitions(gnuradio-qtgui PRIVATE -DENABLE_PYTHON)
+  target_link_libraries(gnuradio-qtgui PRIVATE
     Python::Python
   )
 endif(ENABLE_PYTHON)
-- 
2.20.1

